plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "com.android.library"
}

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
}

group Realm.group
version Realm.version

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

// Common Kotlin configuration
kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation kotlin('reflect')
                 api(project(":cinterop"))
            }
        }

        commonTest {
            dependencies {
                implementation(kotlin("test-common"))
                implementation(kotlin("test-annotations-common"))
            }
        }
    }

    // Note that the Kotlin metadata is here, too.
    // See https://kotlinlang.org/docs/reference/mpp-publish-lib.html
    configure([targets["metadata"], jvm()]) {
        mavenPublication {
            def targetPublication = this
            tasks.withType(AbstractPublishToMaven.class)
                .matching { task -> task.publication == targetPublication }
                .all { task -> onlyIf { t -> t.findProperty("isMainHost") == "true" } }
        }
    }
}

// JVM
kotlin {
    jvm()
}

// Android configuration
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        sourceSets {
            main {
                manifest.srcFile "src/androidMain/AndroidManifest.xml"
                jniLibs.srcDir "src/androidMain/jniLibs"
                androidTest.java.srcDirs += 'src/androidTest/kotlin'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}
kotlin {
    android("android") {
        publishLibraryVariants("release", "debug")
    }
    sourceSets {
        androidMain {
            kotlin.srcDir("src/androidMain/kotlin")
            dependencies {
                api(project(":cinterop"))
            }
        }

        androidTest {
            dependencies {
                implementation(kotlin("test"))
                implementation(kotlin("test-junit"))

                implementation 'junit:junit:4.12'
                implementation 'com.android.support.test:runner:1.0.2'
                implementation 'com.android.support.test:rules:1.0.2'
                implementation(kotlin("reflect:1.4.20-M1-63"))
            }
        }
    }
}

kotlin {
    sourceSets {
        nativeCommon {
            dependencies {
                api(project(":cinterop"))
            }
        }
    }
}

// IOS Configurastion
kotlin {
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    iosX64("ios") {}
    sourceSets {
        iosMain {
            dependsOn nativeCommon
        }
        iosTest {}
    }
}

// Macos configuration
kotlin {
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    macosX64("macos") {}
    sourceSets {
        macosMain {
            dependsOn nativeCommon
        }
        macosTest {}
    }
}
// Needs running emulator
//tasks.named("iosTest") {
//    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 11 Pro Max"
//    dependsOn kotlin.targets.ios.binaries.getTest('DEBUG').linkTaskName
//    group = JavaBasePlugin.VERIFICATION_GROUP
//    description = "Runs tests for target 'ios' on an iOS simulator"
//
//    doLast {
//        def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
//        exec {
//            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
//        }
//    }
//}

configurations {
    compileClasspath
}

// See https://kotlinlang.org/docs/reference/mpp-publish-lib.html
artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = project.hasProperty('bintrayUser') ? bintrayUser : 'noUser'
            password = project.hasProperty('bintrayKey') ? bintrayKey : 'noKey'
        }
        // List fetched from https://medium.com/vmware-end-user-computing/publishing-kotlin-multiplatform-artifacts-to-artifactory-maven-a283ae5912d6
        defaults {
            // TODO Unclear if we should name "iosArm64" and "macosX64" as well?
            publications ("androidDebug", "androidRelease", "ios", "macos", "jvm", "kotlinMultiplatform", "metadata")
        }
    }
}

// Add common POM elements to all publications
// FIXME: I don't know how to access Kotlin inner object classes from Groovy. This is a giant hack. Convert this file
//  to .kts as quickly as possible.
def commonPom = {
    licenses {
        license {
            name Realm.classes.first().name
            url Realm.classes.first().url
            distribution 'repo'
        }
    }
    issueManagement {
        system Realm.classes[1].name
        url Realm.classes[1].url
    }
    scm {
        url Realm.classes.last().url
        connection Realm.classes.last().connection
        developerConnection Realm.classes.last().developerConnection
    }
}
publishing {
    publications.all {
        pom.withXml {
            def root = asNode()
            root.children().last() + commonPom
        }
    }
}

